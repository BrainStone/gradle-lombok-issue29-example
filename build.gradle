import io.franzbecker.gradle.lombok.task.DelombokTask

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "io.franzbecker.gradle-lombok" version "1.10"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.google.guava:guava:22.0"
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")

    classpath += project.configurations.compileOnly
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each { 
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
}

